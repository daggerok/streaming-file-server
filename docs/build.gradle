buildscript {
  ext {
    asciidocBuildDir = "$buildDir/asciidoc"
  }
}

plugins {
  id "org.asciidoctor.convert" version "1.5.7"
//id "org.ajoberstar.git-publish" version "0.2.2"
  id "org.ajoberstar.git-publish" version "0.3.0"
}

asciidoctorj {
  version = "1.5.5"
}

asciidoctor {
  sourceDir = project.file("src")
  sources {
    include "index.adoc"
  }
  outputDir = project.file("$asciidocBuildDir")
  attributes toc: "left",
      idprefix: "",
      docinfo1: "",
      icons: "font",
      idseparator: "-",
      setanchors: "true",
      "source-highlighter": "coderay",
      "toc-title": "Table of Contents",
      "build-gradle": file("build.gradle"),
      "endpoint-url": "https://daggerok.github.io/streaming-file-server"
}

asciidoctor.doLast {
  file("$asciidocBuildDir/html5/404.html").text = file("$asciidocBuildDir/html5/index.html").text
  file("$asciidocBuildDir/html5/.nojekyll").text = "\n"
}

gitPublish {
  //// bash gradlew clean asciidoctor docs \
  ////   -Dorg.ajoberstar.grgit.auth.username=$GITHUB_USERNAME \
  ////   -Dorg.ajoberstar.grgit.auth.password=$GITHUB_PASSWORD
  //repoUri = "https://github.com/daggerok/streaming-file-server.git"
  repoUri = "git@github.com:daggerok/streaming-file-server.git"
  branch = "gh-pages"

  contents {
    from("$asciidocBuildDir/html5")
    from("$projectDir/src/static") {
      into "."
    }
  }

  commitMessage = "Documentation for ${project.group}:${project.name}:$project.version"
}

gitPublishPush.dependsOn asciidoctor
gitPublishPush.shouldRunAfter clean, asciidoctor, build

task docs(dependsOn: gitPublishPush)
